#include <SPI.h>
#include <MFRC522.h>
#include <Wire.h>
#include "RTClib.h"

RTC_DS3231 rtc;
bool rtc_available = false;
const long PHT_OFFSET_SECONDS = 0;  // Adjust this if your RTC is not already on PHT

#define SS_PIN 10
#define RST_PIN 9
MFRC522 mfrc522(SS_PIN, RST_PIN);

// BUZZER ON PIN 8
#define BUZZER_PIN 8

// DATABASE
const String Card_UIDs[] = { "B32B7739", "D7203C02", "D2B78DAB" };
const String Student_Names[] = { "MACASIEB_JULIANA", "TAN_ARLY JAY", "DALISAY_ANNE NICOLE" };
const String Grade_Levels[] = { "12", "12", "12" };
const String Strands[] = { "ICT", "ICT", "ICT" };
const String Sections[] = { "ALPHA", "ALPHA", "ALPHA" };
const int DB_SIZE = sizeof(Card_UIDs) / sizeof(Card_UIDs[0]);

int time_status[DB_SIZE] = { 0 };
String Time_In_Records[DB_SIZE] = { "", "", "" };

int readsuccess;
byte readcard[4];
char str[32] = "";
String StrUID;

struct StudentInfo {
  int index = -1;
  String name = "UNKNOWN";
  String grade = "N/A";
  String strand = "N/A";
  String section = "N/A";
};

// --- HELPERS ---
void array_to_string(byte array[], unsigned int len, char buffer[]) {
  for (unsigned int i = 0; i < len; i++) {
    byte nib1 = (array[i] >> 4) & 0x0F;
    byte nib2 = (array[i] >> 0) & 0x0F;
    buffer[i * 2 + 0] = nib1 < 0xA ? '0' + nib1 : 'A' + nib1 - 0xA;
    buffer[i * 2 + 1] = nib2 < 0xA ? '0' + nib2 : 'A' + nib2 - 0xA;
  }
  buffer[len * 2] = '\0';
}

int getid() {
  if (!mfrc522.PICC_IsNewCardPresent()) return 0;
  if (!mfrc522.PICC_ReadCardSerial()) return 0;
  for (int i = 0; i < 4; i++) { readcard[i] = mfrc522.uid.uidByte[i]; }
  array_to_string(readcard, 4, str);
  StrUID = str;
  mfrc522.PICC_HaltA();
  return 1;
}

StudentInfo get_student_info(String uid) {
  StudentInfo info;
  for (int i = 0; i < DB_SIZE; i++) {
    if (uid == Card_UIDs[i]) {
      info.index = i;
      info.name = Student_Names[i];
      info.grade = Grade_Levels[i];
      info.strand = Strands[i];
      info.section = Sections[i];
      break;
    }
  }
  return info;
}

void setup() {
  pinMode(BUZZER_PIN, OUTPUT);
  Serial.begin(9600);

  if (!rtc.begin()) {
    Serial.println("RTC Not Found");
    rtc_available = false;
  } else {
    rtc_available = true;
    if (rtc.lostPower()) {
      // rtc.adjust(DateTime(F(__DATE__), F(__TIME__)));
    }
  }

  SPI.begin();
  mfrc522.PCD_Init();

  // Ready Beep
  //tone(BUZZER_PIN, 500, 200);
  beep(200);
  //noTone(BUZZER_PIN);

  Serial.println("CLEARDATA");
  Serial.println("LABEL,Date,RFID UID,Name,Grade Level,Strand,Section,TIME IN (PHT),TIME OUT (PHT)");
  Serial.println("Welcome! System Ready...");
}
void beep(int duration) {
  digitalWrite(BUZZER_PIN, HIGH);
  delay(duration);
  digitalWrite(BUZZER_PIN, LOW);
}
void loop() {
  readsuccess = getid();
  if (readsuccess) {
    String date_str = "DATE", time_str = "TIME";

    if (rtc_available) {
      DateTime now = rtc.now();
      // Applying the offset and defining now_pht properly
      DateTime now_pht = now + TimeSpan(PHT_OFFSET_SECONDS);

      date_str = String(now_pht.year()) + "/" + String(now_pht.month()) + "/" + String(now_pht.day());
      char timeBuffer[9];
      sprintf(timeBuffer, "%02d:%02d:%02d", now_pht.hour(), now_pht.minute(), now_pht.second());
      time_str = String(timeBuffer);
    }

    StudentInfo student = get_student_info(StrUID);

    if (student.index != -1) {
      if (time_status[student.index] == 0) {
        // TIME IN
        time_status[student.index] = 1;
        Time_In_Records[student.index] = time_str;
        Serial.println("IN: " + student.name);

        // Happy Beep
        beep(100);
        //tone(BUZZER_PIN, 1500, 200);
        delay(200);

        beep(100);
        //tone(BUZZER_PIN, 1800, 250);
      } else {
        // TIME OUT
        time_status[student.index] = 0;
        String time_in = Time_In_Records[student.index];
        Time_In_Records[student.index] = "";

        Serial.println("OUT: " + student.name);
        // Construct data line for Excel/Serial Monitor
        String log_line = (String) "DATA," + date_str + "," + StrUID + "," + student.name + "," + student.grade + "," + student.strand + "," + student.section + "," + time_in + "," + time_str;
        Serial.println(log_line);

        // Single Tone for Out
        //tone(BUZZER_PIN, 800, 400);
        beep(400);
      }
    } else {
      // UNKNOWN CARD
      Serial.println("UNKNOWN: " + StrUID);
      for (int i = 0; i < 3; i++) {
        beep(50);
        //tone(BUZZER_PIN, 200, 100);
        delay(150);
      }
    }
    delay(500);  // Small delay to prevent double reading
    //noTone(BUZZER_PIN);
  }
}
